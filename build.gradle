group "com.somanyfeeds"
version "1.0-SNAPSHOT"
description "SoManyFeeds a simple feed aggregator"

buildscript {
    ext {
        aspenVersion = "2.0.0"
        kotlinVersion = "1.0.6"
        jacksonVersion = "2.8.5"
        springBootVersion = "1.4.2.RELEASE"
        springVersion = "4.3.4.RELEASE"
        springSocialVersion = "1.1.2.RELEASE"
        slf4jVersion = "1.7.22"
        postgresVersion = "9.4.1212"
        hikariVersion = "2.5.1"
    }

    ext["jackson.version"] = ext.jacksonVersion // tell spring boot to use this version.

    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "org.jetbrains.kotlin:kotlin-allopen:$kotlinVersion"
    }
}

task dependenciesGraphDot {

    mustRunAfter "clean"

    def graphBuildDir = "build/dependenciesGraph"
    def dotFile = file "$graphBuildDir/graph.dot"

    doLast {
        delete graphBuildDir
        mkdir graphBuildDir

        dotFile << "digraph dependencies {\n"

        project.subprojects.forEach { Project subProject ->
            try {

                Configuration compileConfig = subProject.configurations["compile"]

                compileConfig
                        .dependencies
                        .grep { it.respondsTo("getDependencyProject") }
                        .forEach { dotFile << """  "$subProject.name" -> "$it.dependencyProject.name"\n""" }

            } catch (UnknownConfigurationException ignored) {
            }
        }

        dotFile << "}\n"
    }
}

task dependenciesGraph(dependsOn: "dependenciesGraphDot", type: Exec) {
    workingDir "$project.buildDir/dependenciesGraph"
    commandLine "dot", "-O", "-Tpng", "graph.dot"
}

task clean(type: Delete) {
    delete "build"
}

defaultTasks "clean", "build", "dependenciesGraph"
